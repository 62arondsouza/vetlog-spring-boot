buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath("org.flywaydb:flyway-mysql:9.21.1")
  }
}

plugins {
  id 'org.springframework.boot' version '3.1.3'
  id 'io.spring.dependency-management' version '1.1.3'
  id 'org.flywaydb.flyway' version '9.21.1'
  id "org.sonarqube" version "3.3"
  id 'jacoco'
  id 'java'
  id 'com.diffplug.spotless' version '6.21.0'
}

ext {
  gcpVersion = "4.7.1"
  retrofitVersion = '2.9.0'
  mockitoInlineVersion = '4.8.0'
}

group = 'com.jos.dem.vetlog'
version = '1.5.0.1'

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}


repositories {
  mavenCentral()
}

flyway {
  url = 'jdbc:mysql://localhost:3306/vetlog'
}

spotless {
  java {
    target fileTree('.') {
      include '**/*.java'
      exclude '**/build/**', '**/build-*/**'
    }
    toggleOffOn()
    palantirJavaFormat()
    removeUnusedImports()
    trimTrailingWhitespace()
    endWithNewline()
  }
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-aop'
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  implementation "com.google.cloud:spring-cloud-gcp-starter-storage"
  implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
  implementation "com.squareup.retrofit2:converter-jackson:$retrofitVersion"
  implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation platform("com.google.cloud:spring-cloud-gcp-dependencies:$gcpVersion")
  compileOnly 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'
  runtimeOnly 'com.mysql:mysql-connector-j'
  implementation 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
  implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
  implementation group: 'org.hibernate', name: 'hibernate-validator', version: '7.0.4.Final'
  implementation 'net.minidev:json-smart:2.3'
  implementation 'javax.xml.bind:jaxb-api:2.3.1'
  testImplementation "cglib:cglib-nodep:3.2.4"
  testCompileOnly 'org.projectlombok:lombok'
  testAnnotationProcessor 'org.projectlombok:lombok'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.security:spring-security-test'
  testImplementation 'io.projectreactor:reactor-test'
  testImplementation "org.mockito:mockito-inline:$mockitoInlineVersion"
}

jacocoTestReport {
  reports {
    xml.enabled = true
    html.enabled = true
    html.destination file("${buildDir}/jacocoHtml")
  }
}

tasks.named('test') {
  useJUnitPlatform()
  systemProperties = System.properties
}

springBoot {
  buildInfo()
}

bootRun {
  systemProperties = System.properties
}

test.dependsOn("spotlessApply")
bootRun.dependsOn("flywayMigrate")
